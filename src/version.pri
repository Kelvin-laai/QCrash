QCRASH_VERSION_MAJOR        = 2
QCRASH_VERSION_MINOR        = 0
QCRASH_VERSION_PATCH        = 8
win32: QCRASH_VERSION_BUILD = 1


# major.minor.patch.build
QCRASH_VERSION_STR        = "$${QCRASH_VERSION_MAJOR}.$${QCRASH_VERSION_MINOR}.$${QCRASH_VERSION_PATCH}"
win32: QCRASH_VERSION_STR = "$${QCRASH_VERSION_STR}.$${QCRASH_VERSION_BUILD}"

DEFINES        += QCRASH_VERSION_MAJOR=$$QCRASH_VERSION_MAJOR
DEFINES        += QCRASH_VERSION_MINOR=$$QCRASH_VERSION_MINOR
DEFINES        += QCRASH_VERSION_PATCH=$$QCRASH_VERSION_PATCH
win32: DEFINES += QCRASH_VERSION_BUILD=$$QCRASH_VERSION_BUILD
DEFINES        += QCRASH_VERSION_STR='\\"$${QCRASH_VERSION_STR}\\"'

# major.minor.patch.build
PRODUCT_NAME    = "$${TARGET}"
COMPANY_NAME    = ""
win32 {
    CONFIG                  += skip_target_version_ext
    QMAKE_TARGET_COMPANY     = $${COMPANY_NAME}
    QMAKE_TARGET_PRODUCT     = $${PRODUCT_NAME}
    QMAKE_TARGET_DESCRIPTION = "$${PRODUCT_NAME} (Based On Qt$${QT_VERSION})"
    QMAKE_TARGET_COPYRIGHT   = "Copyright Â© 2007-2015 Speedovation."
    RC_LANG                  = 0x0004
}

VERSION = $${QCRASH_VERSION_STR}
